#pragma kernel CSMain

struct GrassProperties
{
    // float3 offset;
    float3 normal;
    float height;
};

// Data Structures
StructuredBuffer<float4x4> _MeshProperties;
RWStructuredBuffer<GrassProperties> _GrassProperties;
AppendStructuredBuffer<uint> _ClipProperties;

// Parameters
float _DepthClipTreshold;
float4x4 _VP;
int _GrassCount;

[numthreads(64, 1, 1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint i = id.x;
    if (i >= _GrassCount) return;

    // Clipping Logic
    bool Clip = false;
    bool HeightClip = false;
    bool ViewClip   = false;
    bool DepthClip  = false;

    if (_GrassProperties[i].height <= 0)
        HeightClip = true;

    float3 positionWS = mul(_MeshProperties[i], float4(0,0,0,1)).xyz;
    float4 positionCS = mul(_VP, float4(positionWS,1));
    float3 ndc = positionCS.xyz / positionCS.w;
    float  treshold = 1.1;
    if (positionCS.w <= 0 || ndc.x < -treshold || ndc.x > treshold || ndc.y < -treshold || ndc.y > treshold)
        ViewClip = true;

    float depth = positionCS.w;
    if (depth > _DepthClipTreshold) 
        DepthClip = true;

    if (HeightClip || ViewClip || DepthClip) 
        Clip = true;
    if (!Clip) _ClipProperties.Append(i);
}